FLHeader
	instanceVariables: #(#preMaterializationActions #additionalObjects #postMaterializationActions #materialization );
	package: #'Fuel-Core'.

FLHeader >> addPostMaterializationAction: aCleanBlockClosure
[
	"The closure may (or not) have one parameter and it will be the materialization."

	aCleanBlockClosure isClean
		ifFalse: [ self
				error: 'Post materializaton actions have to be clean closures. For more details see method BlocKClosure >> #isClean' ].
	postMaterializationActions add: aCleanBlockClosure
]

FLHeader >> addPreMaterializationAction: aCleanBlockClosure
[
	aCleanBlockClosure isClean
		ifFalse: [ self error: 'Pre materializaton actions have to be clean closures. For more details see method BlocKClosure >> #isClean' ].
	preMaterializationActions add: aCleanBlockClosure
]

FLHeader >> additionalObjectAt: key
[
	^ additionalObjects at: key
]

FLHeader >> at: key putAdditionalObject: anObject
[
	"This is useful if we want to attach objects to a package that will also be serialized. The way they are stored is key-value."

	additionalObjects at: key put: anObject
]

FLHeader >> executePostMaterializationActions
[
	postMaterializationActions do: [ :each | each cull: materialization ]
]

FLHeader >> executePreMaterializationActions
[
	preMaterializationActions do: [ :each | each value ]
]

FLHeader >> initialize
[
	additionalObjects := IdentityDictionary new.
	preMaterializationActions := OrderedCollection new.
	postMaterializationActions := OrderedCollection new
]

FLHeader >> isEmpty
[
	^ preMaterializationActions isEmpty and: [ postMaterializationActions isEmpty and: [ additionalObjects isEmpty ] ]
]

FLHeader >> materialization: aMaterialization
[
	"This is just set once the materialization had happened"

	materialization := aMaterialization
]

FLHeader >> postMaterializationActions: listOfPostMaterializationActions
[
	postMaterializationActions := listOfPostMaterializationActions
]

FLHeader >> preMaterializationActions: listOfPreMaterializationActions
[
	preMaterializationActions := listOfPreMaterializationActions
]

