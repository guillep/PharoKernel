PostorderGuide
	superclass: #FileSystemGuide;
	package: #'FileSystem-Core'.

PostorderGuide >> pushTraverse: aReference
pushTraverse: aReference
	self push: (Message selector: #traverse: argument: aReference)

PostorderGuide >> pushVisit: aReference
pushVisit: aReference
	self push: (Message selector: #visit: argument: aReference)

PostorderGuide >> show: aReference
show: aReference
	self pushTraverse: aReference entry.
	self whileNotDoneDo: [ self pop sendTo: self ]

PostorderGuide >> traverse: anEntry
traverse: anEntry
	self pushVisit: anEntry.
	anEntry isDirectory
		ifTrue: [ (self shouldVisitChildrenOf: anEntry)
				ifFalse: [ ^ self ].
			anEntry reference entries reverseDo: [ :each | self pushTraverse: each ] ]

PostorderGuide >> visit: anEntry
visit: anEntry
	anEntry isDirectory
		ifTrue: [ visitor visitDirectory: anEntry ]
		ifFalse: [ visitor visitFile: anEntry ]

