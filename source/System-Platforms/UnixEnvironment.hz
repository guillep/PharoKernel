UnixEnvironment
	superclass: #OSEnvironment;
	package: #'System-Platforms'.

UnixEnvironment >> environ
[
	"Return the address of the array holding the environment variables"

	^ NBExternalAddress value: ((NativeBoost loadSymbol: 'environ' fromModule: NativeBoost CLibrary) nbUInt32AtOffset: 0)
]

UnixEnvironment >> environAt: index
[
	| address |
	address := NBExternalAddress value: (self environ nbUInt32AtOffset: (index - 1) * 4).
	address isNull
		ifTrue: [ ^ nil ].
	^ address readString
]

UnixEnvironment >> keysAndValuesDo: aBlock
[
	| index associationString |
	index := 1.
	[ associationString := self environAt: index.
	associationString ifNil: [ ^ self ].
	self keysAndValuesDo: aBlock withAssociationString: associationString.
	index := index + 1 ]
		repeat
]

UnixEnvironment >> setEnv: nameString value: valueString
[
	"This method calls the Standard C Library getenv() function"

	<primitive: #primitiveNativeCall module: #NativeBoostPlugin error: #errorCode>
	^ self nbCall: #(#int #setenv #(#String #nameString #, #String #valueString #, 1)) module: NativeBoost CLibrary
]

UnixEnvironment class >> isDefaultFor: aPlatform
[
	^ aPlatform isUnix or: [ aPlatform isMacOSX or: [ aPlatform isMacOS ] ]
]

