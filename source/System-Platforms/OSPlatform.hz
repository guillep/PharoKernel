OSPlatform
	instanceVariables: #(#environment );
	classVariables: #(#Current );
	package: #'System-Platforms'.

OSPlatform >> createEnvironment
[
	^ OSEnvironment environmentFor: self
]

OSPlatform >> current
[
	^ self
]

OSPlatform >> environment
[
	^ environment ifNil: [ environment := self createEnvironment ]
]

OSPlatform >> family
[
	"Returns a symbol specific to the platform family (MacOSX, Windows, Unix, RiscOS).
	This may need to be extended if there are new platforms added or significant differences within a platform family arise (as was the case between MacOS 9 and X)."

	self subclassResponsibility
]

OSPlatform >> isMacOS
[
	^ false
]

OSPlatform >> isMacOSX
[
	^ false
]

OSPlatform >> isUnix
[
	^ false
]

OSPlatform >> isWin32
[
	^ false
]

OSPlatform >> isWindows
[
	^ false
]

OSPlatform >> isX11
[
	^ self windowSystemName = 'X11'
]

OSPlatform >> lineEnding
[
	^ self subclassResponsibility
]

OSPlatform >> name
[
	"Return the name of the platform we're running on"

	^ self class currentPlatformName
]

OSPlatform >> platformFamily
[
	^ self family
]

OSPlatform >> platformName
[
	^ self name
]

OSPlatform >> shutDown: quitting
[
	"Pharo is shutting down. If this platform requires specific shutdown code, this is a great place to put it."

	
]

OSPlatform >> startUp: resuming
[
	"Pharo is starting up. If this platform requires specific intialization, this is a great place to put it."

	
]

OSPlatform >> subtype
[
	"Return the subType of the platform we're running on"

	^ Smalltalk vm getSystemAttribute: 1003
]

OSPlatform >> version
[
	"Return the version number string of the platform we're running on"

	^ self class currentVersion
]

OSPlatform >> virtualKey: virtualKeyCode
[
	"Subclass responsibility to override if necessary"

	^ nil
]

OSPlatform >> windowSystemName
[
	"Return the name of the window system currently being used for display."

	^ Smalltalk vm getSystemAttribute: 1005
]

OSPlatform class >> current
[
	"Answer the current platform"

	^ Current
]

OSPlatform class >> currentPlatformName
[
	"Return the name of the platform we're running on"

	^ Smalltalk vm getSystemAttribute: 1001
]

OSPlatform class >> currentVersion
[
	"Return the version number string of the platform we're running on"

	^ (Smalltalk vm getSystemAttribute: 1002) asString
]

OSPlatform class >> determineActivePlatform
[
	"Look for the matching platform class"

	^ self allSubclasses detect: [ :any | any isActivePlatform ]
]

OSPlatform class >> initialize
[
	"Initialize the receiver"

	"OSPlatform initialize"

	Smalltalk
		removeFromStartUpList: self;
		addToStartUpList: self after: Delay;
		removeFromShutDownList: self;
		addToShutDownList: self after: DisplayScreen.
	self startUp: true
]

OSPlatform class >> isActivePlatform
[
	"Answer whether the receiver is the active platform"

	^ false
]

OSPlatform class >> shutDown: quitting
[
	"The system is going down"

	Current ifNotNil: [ Current shutDown: quitting ]
]

OSPlatform class >> startUp: resuming
[
	"Determine the current platform."

	resuming
		ifFalse: [ ^ self ].
	Current := self determineActivePlatform new.
	Current startUp: resuming
]

