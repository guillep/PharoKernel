SaveCommandLineHandler
	superclass: #CommandLineHandler;
	package: #'System-CommandLine'.

SaveCommandLineHandler >> activate
[
	self activateHelp.
	((self hasOption: 'delete-old')
		ifTrue: [ self arguments size = 2 ]
		ifFalse: [ self arguments size = 1 ])
		ifFalse: [ ^ self exitFailure: 'Invalid Arguments' ].
	self saveImage
]

SaveCommandLineHandler >> saveImage
[
	| imageName oldImageFile oldChangesFile res |
	imageName := self arguments detect: [ :arg | arg ~= '--delete-old' ].
	oldImageFile := Smalltalk imageFile resolve.
	oldChangesFile := Smalltalk changesFile resolve.
	(Smalltalk saveAs: imageName)
		ifTrue: [ "rebooting, when executing this, we are already in the renamed image"
			^ self ].	"...otherwise continuing"
	(self hasOption: 'delete-old')
		ifFalse: [ ^ self exitSuccess ].
	oldImageFile delete.
	oldChangesFile delete.
	self exitSuccess
]

SaveCommandLineHandler class >> commandName
[
	^ 'save'
]

SaveCommandLineHandler class >> description
[
	^ 'Rename the image and changes file'
]

