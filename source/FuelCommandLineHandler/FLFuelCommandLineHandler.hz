FLFuelCommandLineHandler
	superclass: #CommandLineHandler;
	instanceVariables: #(#files );
	package: #FuelCommandLineHandler.

FLFuelCommandLineHandler >> activate
[
	self manageFiles.
	(self commandLine hasOption: 'save')
		ifTrue: [ ThreadSafeTranscript install.
			Smalltalk
				addDeferredStartupAction: [ Smalltalk snapshot: true andQuit: true.
					NonInteractiveTranscript new install ] ].
	(self commandLine hasOption: 'quit')
		ifTrue: [ self exitSuccess ]
]

FLFuelCommandLineHandler >> fileExtension
[
	^ self class fileExtension
]

FLFuelCommandLineHandler >> manageFile: aFileReference
[
	aFileReference
		readStreamDo: [ :stream | stream binary.
			FileStream stdout
				nextPutAll: 'Materializing from file: ' , aFileReference fullName;
				lf.
			FLMaterializer new materializeFrom: stream.
			FileStream stdout
				nextPutAll: 'Materialization finished';
				lf ]
]

FLFuelCommandLineHandler >> manageFiles
[
	"Load all the source files in the given array."

	files := (self commandLine allFilesWithExtension: self fileExtension)
		collect: [ :each | (FileSystem disk resolve: each) asFileReference ].
	files ifNil: [ ^ self ].
	files do: [ :reference | self manageFile: reference ]
]

FLFuelCommandLineHandler class >> commandName
[
	^ 'Fuel'
]

FLFuelCommandLineHandler class >> description
[
	^ 'Loads fuel files'
]

FLFuelCommandLineHandler class >> fileExtension
[
	^ '.fuel'
]

FLFuelCommandLineHandler class >> isResponsibleFor: aCommandLine
[
	"This handler is reponsible only for .fuel files"

	(aCommandLine hasFileWithExtension: self fileExtension)
		ifTrue: [ ^ true ].
	^ super isResponsibleFor: aCommandLine
]


	(aCommandLine hasFileWithExtension: self fileExtension)
		ifTrue: [ ^ true ].
	^ super isResponsibleFor: aCommandLine
]

