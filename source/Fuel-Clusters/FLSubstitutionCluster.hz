FLSubstitutionCluster
	superclass: #FLCluster;
	instanceVariables: #(#substitutions #substitutes );
	package: #'Fuel-Clusters'.

FLSubstitutionCluster >> add: anObject substitutedBy: anotherObject traceWith: aAnalysis
[
	substitutions
		at: anObject
		ifAbsent: [ substitutions at: anObject put: anotherObject.
			substitutes add: anotherObject.
			aAnalysis trace: anotherObject ]
]

FLSubstitutionCluster >> initializeAnalyzing
[
	super initializeAnalyzing.
	substitutions := IdentityDictionary new.
	substitutes := IdentitySet new
]

FLSubstitutionCluster >> isSubstitute: anObject
[
	^ substitutes identityIncludes: anObject
]

FLSubstitutionCluster >> objects
[
	"This cluster does not have objects"

	^ #()
]

FLSubstitutionCluster >> printOn: aStream
[
	super printOn: aStream.
	aStream nextPutAll: '->'.
	substitutions printElementsOn: aStream
]

FLSubstitutionCluster >> registerIndexesOn: aDictionary
[
	substitutions
		keysAndValuesDo: [ :anObject :theSubstitution | | substitutionIndex |
			substitutionIndex := aDictionary at: theSubstitution.
			aDictionary at: anObject put: substitutionIndex ]
]

FLSubstitutionCluster >> substitutions
[
	^ substitutions
]

FLSubstitutionCluster class >> clusterBucketIn: aClusterization
[
	^ aClusterization substitutionsBucket
]

