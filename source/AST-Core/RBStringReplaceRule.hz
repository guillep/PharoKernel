RBStringReplaceRule
	superclass: #RBReplaceRule;
	instanceVariables: #(#replaceTree );
	package: #'AST-Core'.

RBStringReplaceRule >> foundMatchFor: aProgramNode
[
	| newNode |
	newNode := replaceTree copyInContext: self context.
	aProgramNode replaceMethodSource: newNode.
	newNode copyCommentsFrom: aProgramNode.
	^ newNode
]

RBStringReplaceRule >> methodReplaceString: replaceString
[
	replaceTree := RBParser parseRewriteMethod: replaceString
]

RBStringReplaceRule >> replaceString: replaceString
[
	replaceTree := RBParser parseRewriteExpression: replaceString.
	searchTree isSequence = replaceTree isSequence
		ifFalse: [ searchTree isSequence
				ifTrue: [ replaceTree := RBSequenceNode statements: (Array with: replaceTree) ]
				ifFalse: [ searchTree := RBSequenceNode statements: (Array with: searchTree) ] ]
]

RBStringReplaceRule >> searchFor: searchString replaceWith: replaceString
[
	self searchString: searchString.
	self replaceString: replaceString
]

RBStringReplaceRule >> searchFor: searchString replaceWith: replaceString when: aBlock
[
	self searchFor: searchString replaceWith: replaceString.
	verificationBlock := aBlock
]

RBStringReplaceRule >> searchForMethod: searchString replaceWith: replaceString
[
	self methodSearchString: searchString.
	self methodReplaceString: replaceString
]

RBStringReplaceRule >> searchForMethod: searchString replaceWith: replaceString when: aBlock
[
	self searchForMethod: searchString replaceWith: replaceString.
	verificationBlock := aBlock
]

RBStringReplaceRule >> searchForTree: aBRProgramNode replaceWith: replaceNode
[
	searchTree := aBRProgramNode.
	replaceTree := replaceNode
]

RBStringReplaceRule >> searchForTree: aBRProgramNode replaceWith: replaceString when: aBlock
[
	self searchForTree: aBRProgramNode replaceWith: replaceString.
	verificationBlock := aBlock
]

RBStringReplaceRule class >> searchFor: searchString replaceWith: replaceString
[
	^ self new searchFor: searchString replaceWith: replaceString
]

RBStringReplaceRule class >> searchFor: searchString replaceWith: replaceString when: aBlock
[
	^ self new searchFor: searchString replaceWith: replaceString when: aBlock
]

RBStringReplaceRule class >> searchForMethod: searchString replaceWith: replaceString
[
	^ self new
		searchForMethod: searchString replaceWith: replaceString;
		yourself
]

RBStringReplaceRule class >> searchForMethod: searchString replaceWith: replaceString when: aBlock
[
	^ self new searchForMethod: searchString replaceWith: replaceString when: aBlock
]

RBStringReplaceRule class >> searchForTree: aRBProgramNode replaceWith: replaceString
[
	^ self new searchForTree: aRBProgramNode replaceWith: replaceString
]

RBStringReplaceRule class >> searchForTree: aRBProgramNode replaceWith: replaceString when: aBlock
[
	^ self new searchForTree: aRBProgramNode replaceWith: replaceString when: aBlock
]

