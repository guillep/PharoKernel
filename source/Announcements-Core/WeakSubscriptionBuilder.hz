WeakSubscriptionBuilder
	instanceVariables: #(#announcer );
	package: #'Announcements-Core'.

WeakSubscriptionBuilder >> announcer: anAnnouncer
[
	announcer := anAnnouncer
]

WeakSubscriptionBuilder >> on: anAnnouncementClass do: aValuable
[
	^ self subscribe: anAnnouncementClass do: aValuable
]

WeakSubscriptionBuilder >> on: anAnnouncementClass send: aSelector to: anObject
[
	^ self subscribe: anAnnouncementClass send: aSelector to: anObject
]

WeakSubscriptionBuilder >> subscribe: anAnnouncementClass do: aValuable
[
	^ announcer
		basicSubscribe:
			(WeakAnnouncementSubscription new
				announcer: announcer;
				announcementClass: anAnnouncementClass;
				valuable: aValuable)
]

WeakSubscriptionBuilder >> subscribe: anAnnouncementClass send: aSelector to: anObject
[
	^ self subscribe: anAnnouncementClass do: (WeakMessageSend receiver: anObject selector: aSelector)
]

WeakSubscriptionBuilder >> weak
[
	"already weak"

	^ self
]

WeakSubscriptionBuilder >> when: anAnnouncementClass do: aValuable
[
	^ self subscribe: anAnnouncementClass do: aValuable
]

WeakSubscriptionBuilder class >> on: announcer
[
	^ self new announcer: announcer
]

