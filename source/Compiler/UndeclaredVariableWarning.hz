UndeclaredVariableWarning
	superclass: #Warning;
	instanceVariables: #(#name #selector #class );
	package: #Compiler.

UndeclaredVariableWarning >> defaultAction
[
	"The user should be notified of the occurrence of an exceptional occurrence and
	 given an option of continuing or aborting the computation. The description of the
	 occurrence should include any text specified as the argument of the #signal: message."

	selector ifNotNil: [ self crTrace: class name , '>>' , selector , ' ' ] ifNil: [ self traceCr: '' ].
	self traceCr: '(' , name , ' is Undeclared) '.
	^ true
]

UndeclaredVariableWarning >> defaultResumeValue
[
	"Answer the value that by default should be returned if the exception is resumed"

	^ true
]

UndeclaredVariableWarning >> messageText
[
	^ String
		streamContents: [ :s | selector ifNotNil: [ s
						print: class;
						nextPutAll: '>>';
						print: selector;
						space ] ifNil: [ s cr ].
			s
				nextPut: $(;
				print: name;
				nextPutAll: ' is Undeclared) ' ]
]

UndeclaredVariableWarning >> name: aString selector: aSymbolOrNil class: aBehavior
[
	name := aString.
	selector := aSymbolOrNil.
	class := aBehavior
]

