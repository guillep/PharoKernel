TextFontReference
	superclass: #TextFontChange;
	instanceVariables: #(#font );
	package: #'Text-Core'.

TextFontReference >> = other
[
	^ other class == self class and: [ other font = font ]
]

TextFontReference >> emphasizeScanner: scanner
[
	"Set the actual font for text display"

	scanner setActualFont: font
]

TextFontReference >> font
[
	^ font
]

TextFontReference >> forFontInStyle: aTextStyle do: aBlock
[
	aBlock value: font
]

TextFontReference >> hash
[
	"#hash is re-implemented because #= is re-implemented"

	^ font hash
]

TextFontReference >> printOn: aStream
[
	aStream
		nextPutAll: 'a TextFontReference(';
		print: font;
		nextPut: $)
]

TextFontReference >> toFont: aFont
[
	font := aFont
]

TextFontReference >> writeScanOn: strm
[
	strm nextPut: $F.
	strm
		nextPutAll: font familyName;
		nextPut: $#.
	font height printOn: strm
]

TextFontReference class >> toFont: aFont
[
	^ self new toFont: aFont
]

