ZipStringMember
	superclass: #ZipArchiveMember;
	instanceVariables: #(#contents #stream );
	package: #Compression.

ZipStringMember >> contents
[
	^ contents
]

ZipStringMember >> contents: aString
[
	contents := aString.
	compressedSize := uncompressedSize := aString size.	"set the file date to now"
	self modifiedAt: DateAndTime now
]

ZipStringMember >> initialize
[
	super initialize.
	self contents: ''.
	compressionMethod := desiredCompressionMethod := CompressionStored
]

ZipStringMember >> readRawChunk: n
[
	^ stream next: n
]

ZipStringMember >> rewindData
[
	super rewindData.
	stream := contents readStream.
	readDataRemaining := contents size
]

ZipStringMember class >> newFrom: aString named: aFileName
[
	^ self new
		contents: aString;
		localFileName: aFileName;
		yourself
]

