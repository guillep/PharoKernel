MemoryFileSystemDirectory
	superclass: #MemoryFileSystemEntry;
	instanceVariables: #(#entries );
	package: #'FileSystem-Memory'.

MemoryFileSystemDirectory >> ensureCreateDirectory: aDirectoryName
[
	^ self fileEntryAt: aDirectoryName put: (MemoryFileSystemDirectory named: aDirectoryName)
]

MemoryFileSystemDirectory >> ensureCreateFile: aFileName
[
	^ self fileEntryAt: aFileName put: (MemoryFileSystemFile named: aFileName)
]

MemoryFileSystemDirectory >> entries
[
	^ entries
]

MemoryFileSystemDirectory >> fileEntriesDo: aBlock
[
	entries keys sort do: [ :fileName | aBlock value: (entries at: fileName) ]
]

MemoryFileSystemDirectory >> fileEntriesIncludes: aFileName
[
	^ entries includesKey: aFileName
]

MemoryFileSystemDirectory >> fileEntryAt: aFileName
[
	^ entries at: aFileName
]

MemoryFileSystemDirectory >> fileEntryAt: aFileName ifAbsent: aBlock
[
	^ entries at: aFileName ifAbsent: aBlock
]

MemoryFileSystemDirectory >> fileEntryAt: aFileName ifPresent: aBlock
[
	^ entries at: aFileName ifPresent: aBlock
]

MemoryFileSystemDirectory >> fileEntryAt: aFileName put: anEntry
[
	^ entries at: aFileName ifAbsentPut: [ self modified.
			anEntry ]
]

MemoryFileSystemDirectory >> fileEntryRemove: aFileName
[
	^ self fileEntryRemove: aFileName ifAbsent: [ FileDoesNotExist signalWith: aFileName ]
]

MemoryFileSystemDirectory >> fileEntryRemove: aFileName ifAbsent: absentBlock
[
	| deletedEntry |
	deletedEntry := entries removeKey: aFileName ifAbsent: [ ^ absentBlock value ].
	modificationTime := DateAndTime now.
	^ deletedEntry
]

MemoryFileSystemDirectory >> fileSize
[
	^ 0
]

MemoryFileSystemDirectory >> initialize
[
	super initialize.
	entries := Dictionary new
]

MemoryFileSystemDirectory >> isDirectory
[
	^ true
]

