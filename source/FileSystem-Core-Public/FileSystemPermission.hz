FileSystemPermission
	instanceVariables: #(#posixPermission );
	package: #'FileSystem-Core-Public'.

FileSystemPermission >> < other
[
	^ posixPermission < other posixPermission
]

FileSystemPermission >> groupExecute
[
	^ self permissionBitAt: 4
]

FileSystemPermission >> groupRead
[
	^ self permissionBitAt: 6
]

FileSystemPermission >> groupWrite
[
	^ self permissionBitAt: 5
]

FileSystemPermission >> initialize: aNumber
[
	posixPermission := aNumber.
	self initialize
]

FileSystemPermission >> isReadable
[
	^ self ownerRead
]

FileSystemPermission >> isWritable
[
	^ self ownerWrite
]

FileSystemPermission >> otherExecute
[
	^ self permissionBitAt: 1
]

FileSystemPermission >> otherRead
[
	^ self permissionBitAt: 3
]

FileSystemPermission >> otherWrite
[
	^ self permissionBitAt: 2
]

FileSystemPermission >> ownerExecute
[
	^ self permissionBitAt: 7
]

FileSystemPermission >> ownerRead
[
	^ self permissionBitAt: 9
]

FileSystemPermission >> ownerWrite
[
	^ self permissionBitAt: 8
]

FileSystemPermission >> permissionBitAt: bitIndex
[
	^ (posixPermission bitAt: bitIndex) == 1
]

FileSystemPermission >> printOn: aStream
[
	aStream
		nextPut: (self ownerRead
						ifTrue: [ $r ]
						ifFalse: [ $- ]);
		nextPut: (self ownerWrite
						ifTrue: [ $w ]
						ifFalse: [ $- ]);
		nextPut: (self ownerExecute
						ifTrue: [ $x ]
						ifFalse: [ $- ]);
		nextPut: (self groupRead
						ifTrue: [ $r ]
						ifFalse: [ $- ]);
		nextPut: (self groupWrite
						ifTrue: [ $w ]
						ifFalse: [ $- ]);
		nextPut: (self groupExecute
						ifTrue: [ $x ]
						ifFalse: [ $- ]);
		nextPut: (self otherRead
						ifTrue: [ $r ]
						ifFalse: [ $- ]);
		nextPut: (self otherWrite
						ifTrue: [ $w ]
						ifFalse: [ $- ]);
		nextPut: (self otherExecute
						ifTrue: [ $x ]
						ifFalse: [ $- ])	"Owner"	"Group"	"Other"
]

FileSystemPermission class >> new
[
	self error: 'Should not be called. Use #posixPermission: instead'
]

FileSystemPermission class >> posixPermissions: aNumber
[
	^ self basicNew
		initialize: aNumber;
		yourself
]

