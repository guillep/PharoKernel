NumberParser
	instanceVariables: #(#sourceStream #base #neg #integerPart #fractionPart #exponent #scale #nDigits #lastNonZero #requestor #failBlock );
	package: #Kernel.

NumberParser >> allowPlusSign
[allowPlusSign
	"return a boolean indicating if plus sign is allowed or not"

	^ false
]

NumberParser >> allowPlusSignInExponent
[allowPlusSignInExponent
	"return a boolean indicating if plus sign is allowed or not in exponent"

	^ self allowPlusSign
]

NumberParser >> expected: aString
[expected: aString
	| errorString |
	errorString := aString , ' expected'.
	requestor isNil
		ifFalse: [ requestor notify: errorString at: sourceStream position + 1 in: sourceStream ].
	failBlock ifNotNil: [ ^ failBlock cull: errorString cull: sourceStream position + 1 ].
	self error: 'Reading a number failed: ' , errorString
]

NumberParser >> exponentLetters
[exponentLetters
	"answer the list of possible exponents for Numbers.
	Note: this parser will not honour precision attached to the exponent.
	different exponent do not lead to different precisions.
	only IEEE 754 floating point numbers will be created"

	^ 'edq'
]

NumberParser >> fail
[fail
	failBlock ifNotNil: [ ^ failBlock value ].
	self error: 'Reading a number failed'
]

NumberParser >> failBlock: aBlockOrNil
[failBlock: aBlockOrNil
	failBlock := aBlockOrNil
]

NumberParser >> makeFloatFromMantissa: m exponent: k base: aRadix
[makeFloatFromMantissa: m exponent: k base: aRadix
	"Convert infinite precision arithmetic into Floating point.
	This alogrithm rely on correct IEEE rounding mode
	being implemented in Integer>>asFloat and Fraction>>asFloat"

	^ (k positive
		ifTrue: [ m * (aRadix raisedToInteger: k) ]
		ifFalse: [ Fraction numerator: m denominator: (aRadix raisedToInteger: k negated) ]) asFloat
]

NumberParser >> makeIntegerOrScaledInteger
[makeIntegerOrScaledInteger
	"at this point, there is no digit, nor fractionPart.
	maybe it can be a scaled decimal with fraction omitted..."

	neg
		ifTrue: [ integerPart := integerPart negated ].
	self readExponent
		ifTrue: [ ^ integerPart * (base raisedToInteger: exponent) ].
	self readScale
		ifTrue: [ ^ integerPart asScaledDecimal: scale ].
	^ integerPart
]

NumberParser >> makeScaledDecimalWithNumberOfNonZeroFractionDigits: numberOfNonZeroFractionDigits andNumberOfTrailingZeroInFractionPart: numberOfTrailingZeroInFractionPart
[makeScaledDecimalWithNumberOfNonZeroFractionDigits: numberOfNonZeroFractionDigits andNumberOfTrailingZeroInFractionPart: numberOfTrailingZeroInFractionPart
	"at this point integerPart fractionPart and scale have been read out (in inst var).
	Form a ScaledDecimal.
	Care of eliminating trailing zeroes from the fractionPart"

	| decimalMultiplier decimalFraction |
	decimalMultiplier := base raisedToInteger: numberOfNonZeroFractionDigits.
	decimalFraction := (integerPart * decimalMultiplier
		+ (fractionPart // (base raisedTo: numberOfTrailingZeroInFractionPart))) / decimalMultiplier.
	neg
		ifTrue: [ decimalFraction := decimalFraction negated ].
	^ decimalFraction asScaledDecimal: scale
]

NumberParser >> nextElementaryLargeIntegerBase: aRadix
[nextElementaryLargeIntegerBase: aRadix
	"Form an unsigned integer with incoming digits from sourceStream.
	Return this integer, or zero if no digits found.
	Stop reading if end of digits or if a LargeInteger is formed.
	Count the number of digits and the position of lastNonZero digit and store them in instVar."

	| value digit char |
	value := 0.
	nDigits := 0.
	lastNonZero := 0.
	[ value isLarge
		or: [ (char := sourceStream next) == nil
				or: [ digit := char digitValue.
					(0 > digit or: [ digit >= aRadix ]) and: [ sourceStream skip: -1.
							true ] ] ] ]
		whileFalse: [ nDigits := nDigits + 1.
			0 = digit
				ifFalse: [ lastNonZero := nDigits ].
			value := value * aRadix + digit ].
	^ value
]

NumberParser >> nextFraction
[nextFraction
	| numerator denominator |
	numerator := self nextInteger.
	(sourceStream peekFor: $/)
		ifFalse: [ ^ numerator ].
	denominator := self nextInteger.
	^ numerator / denominator
]

NumberParser >> nextInteger
[nextInteger
	"Main method for reading an Integer.
	This won't try to read a Float nor a ScaledDecimal."

	| numberOfTrailingZeroInIntegerPart |
	base := 10.
	neg := self peekSignIsMinus.
	integerPart := self nextUnsignedIntegerOrNilBase: base.
	integerPart ifNil: [ self fail ].
	numberOfTrailingZeroInIntegerPart := nDigits - lastNonZero.
	(sourceStream peekFor: $r)
		ifTrue: [ | oldNeg "<base>r<integer>" pos |
			pos := sourceStream position.
			(base := integerPart) < 2
				ifTrue: [ "A radix currently need to be greater than 1, ungobble the r and return the integer part"
					sourceStream skip: -1.
					^ neg
						ifTrue: [ base negated ]
						ifFalse: [ base ] ].
			oldNeg := neg.
			self peekSignIsMinus
				ifTrue: [ neg := neg not ].
			integerPart := self nextUnsignedIntegerOrNilBase: base.
			integerPart ifNil: [ self fail ].
			numberOfTrailingZeroInIntegerPart := nDigits - lastNonZero ].
	neg
		ifTrue: [ integerPart := integerPart negated ].
	self readExponent
		ifTrue: [ ^ integerPart * (base raisedToInteger: exponent) ].
	^ integerPart
]

NumberParser >> nextIntegerBase: aRadix
[nextIntegerBase: aRadix
	"Form an integer with following digits.
	Fail if no digit found"

	| isNeg value |
	isNeg := self peekSignIsMinus.
	value := self nextUnsignedIntegerBase: aRadix.
	^ isNeg
		ifTrue: [ value negated ]
		ifFalse: [ value ]
]

NumberParser >> nextIntegerBase: aRadix ifFail: aBlock
[nextIntegerBase: aRadix ifFail: aBlock
	"Form an integer with optional sign and following digits from sourceStream."

	| isNeg value |
	isNeg := self peekSignIsMinus.
	value := self nextUnsignedIntegerOrNilBase: aRadix.
	value ifNil: [ ^ aBlock value ].
	^ isNeg
		ifTrue: [ value negated ]
		ifFalse: [ value ]
]

NumberParser >> nextLargeIntegerBase: aRadix nPackets: nPackets
[nextLargeIntegerBase: aRadix nPackets: nPackets
	"Form a Large integer with incoming digits from sourceStream.
	Return this integer, or zero if no digits found.
	Stop reading when no more digits or when nPackets elementary LargeInteger have been encountered.
	Count the number of digits and the lastNonZero digit and store them in instVar"

	| high nDigitsHigh low nDigitsLow halfPackets |
	halfPackets := nPackets bitShift: -1.
	halfPackets = 0
		ifTrue: [ ^ self nextElementaryLargeIntegerBase: aRadix ].
	high := self nextLargeIntegerBase: aRadix nPackets: halfPackets.
	high isLarge
		ifFalse: [ ^ high ].
	nDigitsHigh := nDigits.
	low := self nextLargeIntegerBase: aRadix nPackets: halfPackets.
	nDigitsLow := nDigits.
	nDigits := nDigitsHigh + nDigitsLow.
	lastNonZero = 0
		ifFalse: [ lastNonZero := lastNonZero + nDigitsHigh ].
	^ high * (aRadix raisedToInteger: nDigitsLow) + low
]

NumberParser >> nextNumber
[nextNumber
	"main method for reading a number.
	This one can read Float Integer and ScaledDecimal"

	| numberOfTrailingZeroInIntegerPart |
	base := 10.
	neg := self peekSignIsMinus.
	integerPart := self nextUnsignedIntegerOrNilBase: base.
	integerPart
		ifNil: [ 
			"This is not a regular number beginning with a digit		It is time to check for exceptional condition NaN and Infinity"
			^ self readNamedFloatOrFail ].
	numberOfTrailingZeroInIntegerPart := nDigits - lastNonZero.
	(sourceStream peekFor: $r)
		ifTrue: [ "<base>r<integer>"
			(base := integerPart) < 2
				ifTrue: [ sourceStream skip: -1.
					^ self expected: 'an integer greater than 1 as valid radix' ].
			self peekSignIsMinus
				ifTrue: [ neg := neg not ].
			integerPart := self nextUnsignedIntegerBase: base.
			numberOfTrailingZeroInIntegerPart := nDigits - lastNonZero ].
	^ (sourceStream peekFor: $.)
		ifTrue: [ self readNumberWithFractionPartNumberOfTrailingZeroInIntegerPart: numberOfTrailingZeroInIntegerPart ]
		ifFalse: [ self makeIntegerOrScaledInteger ]
]

NumberParser >> nextNumberBase: b
[nextNumberBase: b
	"Method for reading a number without radix prefix.
	This one can read Float Integer and ScaledDecimal"

	| numberOfTrailingZeroInIntegerPart |
	base := b.
	neg := sourceStream peekFor: $-.
	integerPart := self nextUnsignedIntegerOrNilBase: base.
	integerPart
		ifNil: [ 
			"This is not a regular number beginning with a digit		It is time to check for exceptional condition NaN and Infinity"
			^ self readNamedFloatOrFail ].
	numberOfTrailingZeroInIntegerPart := nDigits - lastNonZero.
	^ (sourceStream peekFor: $.)
		ifTrue: [ self readNumberWithFractionPartNumberOfTrailingZeroInIntegerPart: numberOfTrailingZeroInIntegerPart ]
		ifFalse: [ self makeIntegerOrScaledInteger ]
]

NumberParser >> nextScaledDecimal
[nextScaledDecimal
	"Main method for reading a (scaled) decimal number.
	Good Gracious, do not accept a decimal in another base than 10!
	In other words, do not accept radix notation like 2r1.1, even not 10r5.3
	Do not accept exponent notation neither, like 1.0e-3"

	| numberOfNonZeroFractionDigits numberOfTrailingZeroInFractionPart |
	base := 10.
	neg := sourceStream peekFor: $-.
	integerPart := self nextUnsignedIntegerBase: base.
	(sourceStream peekFor: $.)
		ifTrue: [ fractionPart := self nextUnsignedIntegerOrNilBase: base.
			fractionPart
				ifNil: [ "Oops, the decimal point seems not part of this number"
					sourceStream skip: -1.
					^ neg
						ifTrue: [ integerPart negated asScaledDecimal: 0 ]
						ifFalse: [ integerPart asScaledDecimal: 0 ] ].
			numberOfNonZeroFractionDigits := lastNonZero.
			numberOfTrailingZeroInFractionPart := nDigits - lastNonZero.
			self readScale
				ifFalse: [ "No scale were provided. use number of digits after decimal point as scale"
					scale := nDigits ].
			^ self
				makeScaledDecimalWithNumberOfNonZeroFractionDigits: numberOfNonZeroFractionDigits
				andNumberOfTrailingZeroInFractionPart: numberOfTrailingZeroInFractionPart ].
	self readScale.
	neg
		ifTrue: [ integerPart := integerPart negated ].
	^ integerPart asScaledDecimal: scale
]

NumberParser >> nextUnsignedIntegerBase: aRadix
[nextUnsignedIntegerBase: aRadix
	"Form an unsigned integer with incoming digits from sourceStream.
	Fail if no digit found.
	Count the number of digits and the lastNonZero digit and store int in instVar "

	| value |
	value := self nextUnsignedIntegerOrNilBase: aRadix.
	value ifNil: [ ^ self expected: ('a digit between 0 and ' copyWith: (Character digitValue: aRadix - 1)) ].
	^ value
]

NumberParser >> nextUnsignedIntegerBase: aRadix ifFail: errorBlock
[nextUnsignedIntegerBase: aRadix ifFail: errorBlock
	"Form an unsigned integer with incoming digits from sourceStream.
	Answer this integer, or execute errorBlock if no digit found.
	Count the number of digits and the position of lastNonZero digit and store them in instVar"

	| value |
	value := self nextUnsignedIntegerOrNilBase: aRadix.
	value ifNil: [ ^ errorBlock value ].
	^ value
]

NumberParser >> nextUnsignedIntegerOrNilBase: aRadix
[nextUnsignedIntegerOrNilBase: aRadix
	"Form an unsigned integer with incoming digits from sourceStream.
	Answer this integer, or nil if no digit found.
	Count the number of digits and the position of lastNonZero digit and store them in instVar"

	| nPackets high nDigitsHigh lastNonZeroHigh low |
	"read no more digits than one elementary LargeInteger"
	high := self nextElementaryLargeIntegerBase: aRadix.
	nDigits = 0
		ifTrue: [ ^ nil ].	"Not enough digits to form a LargeInteger, stop iteration"
	high isLarge
		ifFalse: [ ^ high ].	"We now have to engage arithmetic with LargeInteger	Decompose the integer in a high and low packets of growing size:"
	nPackets := 1.
	nDigitsHigh := nDigits.
	lastNonZeroHigh := lastNonZero.
	[ low := self nextLargeIntegerBase: aRadix nPackets: nPackets.
	high := high * (aRadix raisedToInteger: nDigits) + low.
	lastNonZero = 0
		ifFalse: [ lastNonZeroHigh := lastNonZero + nDigitsHigh ].
	nDigitsHigh := nDigitsHigh + nDigits.
	low isLarge ]
		whileTrue: [ nPackets := nPackets * 2 ].
	nDigits := nDigitsHigh.
	lastNonZero := lastNonZeroHigh.
	^ high
]

NumberParser >> on: aStringOrStream
[on: aStringOrStream
	sourceStream := aStringOrStream isString
		ifTrue: [ aStringOrStream readStream ]
		ifFalse: [ aStringOrStream ].
	base := 10.
	neg := false.
	integerPart := fractionPart := exponent := scale := 0.
	requestor := failBlock := nil
]

NumberParser >> peekSignIsMinus
[peekSignIsMinus
	"Peek an optional sign from sourceStream.
	Answer true if it is minus sign"

	| isMinus |
	isMinus := sourceStream peekFor: $-.
	isMinus
		ifFalse: [ self allowPlusSign
				ifTrue: [ sourceStream peekFor: $+ ] ].
	^ isMinus
]

NumberParser >> readExponent
[readExponent
	"read the exponent if any (stored in instVar).
	Answer true if found, answer false if none.
	If exponent letter is not followed by a digit,
	this is not considered as an error.
	Exponent are always read in base 10."

	| eneg epos |
	exponent := 0.
	sourceStream atEnd
		ifTrue: [ ^ false ].
	(self exponentLetters includes: sourceStream peek)
		ifFalse: [ ^ false ].
	sourceStream next.
	eneg := sourceStream peekFor: $-.
	epos := eneg not and: [ self allowPlusSignInExponent and: [ sourceStream peekFor: $+ ] ].
	exponent := self nextUnsignedIntegerOrNilBase: 10.
	exponent
		ifNil: [ "Oops, there was no digit after the exponent letter.Ungobble the letter"
			exponent := 0.
			sourceStream skip: ((eneg or: [ epos ])
						ifTrue: [ -2 ]
						ifFalse: [ -1 ]).
			^ false ].
	eneg
		ifTrue: [ exponent := exponent negated ].
	^ true
]

NumberParser >> readNamedFloatOrFail
[readNamedFloatOrFail
	"This method is used when there is no digit encountered:
	It try and read a named Float NaN or Infinity.
	Negative sign for -Infinity has been read before sending this method, and is indicated in the neg inst.var.
	Fail if no named Float is found"

	neg
		ifFalse: [ (sourceStream nextMatchAll: 'NaN')
				ifTrue: [ ^ Float nan ] ].
	(sourceStream nextMatchAll: 'Infinity')
		ifTrue: [ ^ neg
				ifTrue: [ Float infinity negated ]
				ifFalse: [ Float infinity ] ].
	^ self expected: 'a digit between 0 and ' , (String with: (Character digitValue: base - 1))
]

NumberParser >> readNumberWithFractionPartNumberOfTrailingZeroInIntegerPart: numberOfTrailingZeroInIntegerPart
[readNumberWithFractionPartNumberOfTrailingZeroInIntegerPart: numberOfTrailingZeroInIntegerPart
	"at this stage, sign integerPart and a decimal point have been read.
	try and form a number with a fractionPart"

	| numberOfNonZeroFractionDigits numberOfTrailingZeroInFractionPart mantissa value |
	fractionPart := self nextUnsignedIntegerOrNilBase: base.
	fractionPart
		ifNil: [ "No fractionPart found,ungobble the decimal point and return the integerPart"
			sourceStream skip: -1.
			^ neg
				ifTrue: [ integerPart negated ]
				ifFalse: [ integerPart ] ].
	numberOfNonZeroFractionDigits := lastNonZero.
	numberOfTrailingZeroInFractionPart := nDigits - lastNonZero.
	self readExponent
		ifFalse: [ self readScale
				ifTrue: [ ^ self
						makeScaledDecimalWithNumberOfNonZeroFractionDigits: numberOfNonZeroFractionDigits
						andNumberOfTrailingZeroInFractionPart: numberOfTrailingZeroInFractionPart ] ].
	fractionPart isZero
		ifTrue: [ mantissa := integerPart // (base raisedToInteger: numberOfTrailingZeroInIntegerPart).
			exponent := exponent + numberOfTrailingZeroInIntegerPart ]
		ifFalse: [ mantissa := integerPart * (base raisedToInteger: numberOfNonZeroFractionDigits)
				+ (fractionPart // (base raisedToInteger: numberOfTrailingZeroInFractionPart)).
			exponent := exponent - numberOfNonZeroFractionDigits ].
	value := self makeFloatFromMantissa: mantissa exponent: exponent base: base.
	^ neg
		ifTrue: [ value isZero
				ifTrue: [ Float negativeZero ]
				ifFalse: [ value negated ] ]
		ifFalse: [ value ]
]

NumberParser >> readScale
[readScale
	"read the scale if any (stored in instVar).
    Answer true if found, answer false if none.
    If scale letter is not followed by a digit, this is not considered as an error.
    Scales are always read in base 10, though i do not see why..."

	scale := 0.
	sourceStream atEnd
		ifTrue: [ ^ false ].
	(sourceStream peekFor: $s)
		ifFalse: [ ^ false ].
	scale := self nextUnsignedIntegerOrNilBase: 10.
	scale
		ifNil: [ scale := 0.
			(sourceStream peek ifNil: [ false ] ifNotNil: [ :nextChar | nextChar isLetter ])
				ifTrue: [ sourceStream skip: -1.	"ungobble the s"
					^ false ]
				ifFalse: [ ^ true ] ].
	^ true
]

NumberParser >> requestor: anObjectOrNil
[requestor: anObjectOrNil
	requestor := anObjectOrNil
]

NumberParser class >> on: aStringOrStream
[on: aStringOrStream
	^ self new on: aStringOrStream
]

NumberParser class >> parse: aStringOrStream
[parse: aStringOrStream
	^ self new
		on: aStringOrStream;
		nextNumber
]

NumberParser class >> parse: aStringOrStream onError: failBlock
[parse: aStringOrStream onError: failBlock
	^ self new
		on: aStringOrStream;
		failBlock: failBlock;
		nextNumber
]

NumberParser class >> squeezeNumberOutOfString: stringOrStream
[squeezeNumberOutOfString: stringOrStream
	"Try and find a number in this string. First, look if the string 
	starts with a number. Then, see if it ends with a number. Then,
	remove a character from the front and see if the remaining 
	string makes a number. Repeat the process until no characters
	are left or the number has been found. As soon as a number is
	found, it is returned. Otherwise, the method fails."

	^ self squeezeNumberOutOfString: stringOrStream onError: [ self error: 'Reading a number failed' ]
]

NumberParser class >> squeezeNumberOutOfString: stringOrStream onError: errorBlock
[squeezeNumberOutOfString: stringOrStream onError: errorBlock
	"Try and find a number in this string. First, look if the string 
	starts with a number. Then, see if it ends with a number. Then,
	remove a character from the front and see if the remaining 
	string makes a number. Repeat the process until no characters
	are left or the number has been found. As soon as a number is
	found, it is returned. Otherwise, the method fails."

	| string |
	string := stringOrStream.
	stringOrStream size
		timesRepeat: [ (self parse: string onError: [ nil ]) ifNotNil: [ :result | ^ result ].
			string := string allButFirst ].
	^ errorBlock value
]

