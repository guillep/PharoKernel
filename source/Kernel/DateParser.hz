DateParser
	instanceVariables: #(#inputStream #pattern #patternStream #char #year #month #day #invalidPattern );
	package: #Kernel.

DateParser >> convertTwoDigitsYear
[
	(year between: 0 and: 99)
		ifTrue: [ year := self currentMillenium + year ]
]

DateParser >> createDate
[
	^ Date year: year month: month day: day
]

DateParser >> currentMillenium
[
	^ (Date current year / 100) asInteger * 100
]

DateParser >> initializeParsing
[
	invalidPattern := false.
	patternStream := pattern readStream
]

DateParser >> initializeReadingFrom: anInputStream pattern: aPattern
[
	inputStream := anInputStream.
	pattern := aPattern
]

DateParser >> isDayPattern
[
	^ char = $d
]

DateParser >> isDoneParsing
[
	^ patternStream atEnd or: [ inputStream atEnd or: [ invalidPattern ] ]
]

DateParser >> isEscape
[
	^ char = $\
]

DateParser >> isFourDigitYearPattern
[
	^ patternStream nextMatchAll: 'yyy'
]

DateParser >> isInvalidPattern
[
	^ year isNil or: [ month isNil or: [ day isNil or: [ invalidPattern ] ] ]
]

DateParser >> isMonthPattern
[
	^ char = $m
]

DateParser >> isTwoDigitDayPattern
[
	^ patternStream peekFor: $d
]

DateParser >> isTwoDigitMonthPattern
[
	^ patternStream peekFor: $m
]

DateParser >> isTwoDigitYearPattern
[
	^ patternStream peekFor: $y
]

DateParser >> isYearPattern
[
	^ char = $y
]

DateParser >> parse
[
	self initializeParsing.
	[ self isDoneParsing ] whileFalse: [ self parseNextPattern ].
	self isInvalidPattern
		ifTrue: [ ^ nil ].
	self convertTwoDigitsYear.
	^ self createDate
]

DateParser >> parseDay
[
	self isTwoDigitDayPattern
		ifTrue: [ ^ self parseTwoDigitDay ].
	self parseVariableDigitDay
]

DateParser >> parseEscapePattern
[
	inputStream next = patternStream next
		ifFalse: [ invalidPattern := true ]
]

DateParser >> parseFourDigitYear
[
	year := (inputStream next: 4) asInteger
]

DateParser >> parseMonth
[
	self isTwoDigitMonthPattern
		ifTrue: [ ^ self parseTwoDigitMonth ].
	self parseVariableDigitMonth
]

DateParser >> parseNextPattern
[
	self readNextChar.
	self isEscape
		ifTrue: [ ^ self parseEscapePattern ].
	self isYearPattern
		ifTrue: [ ^ self parseYear ].
	self isMonthPattern
		ifTrue: [ ^ self parseMonth ].
	self isDayPattern
		ifTrue: [ ^ self parseDay ].
	self parseSameChar
]

DateParser >> parseSameChar
[
	inputStream next = char
		ifFalse: [ invalidPattern := true ]
]

DateParser >> parseTwoDigitDay
[
	day := (inputStream next: 2) asInteger
]

DateParser >> parseTwoDigitMonth
[
	month := (inputStream next: 2) asInteger
]

DateParser >> parseTwoDigitYear
[
	year := (inputStream next: 2) asInteger
]

DateParser >> parseVariableDigitDay
[
	day := Integer readFrom: inputStream
]

DateParser >> parseVariableDigitMonth
[
	month := Integer readFrom: inputStream
]

DateParser >> parseVariableDigitYear
[
	year := Integer readFrom: inputStream
]

DateParser >> parseYear
[
	self isFourDigitYearPattern
		ifTrue: [ ^ self parseFourDigitYear ].
	self isTwoDigitYearPattern
		ifTrue: [ ^ self parseTwoDigitYear ].
	self parseVariableDigitYear
]

DateParser >> readNextChar
[
	char := patternStream next
]

DateParser class >> readingFrom: anInputStream pattern: aPattern
[
	"See class comment for pattern description
	self comment"

	^ self new initializeReadingFrom: anInputStream pattern: aPattern
]

